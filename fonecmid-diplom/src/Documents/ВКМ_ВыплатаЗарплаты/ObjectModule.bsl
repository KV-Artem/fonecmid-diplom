#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	Для Каждого ТекСтрокаВыплаты Из Выплаты Цикл
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Сотрудник = ТекСтрокаВыплаты.Сотрудник;
		Движение.Сумма = ТекСтрокаВыплаты.Сумма;
	КонецЦикла;	
	
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.БлокироватьДляИзменения = Истина;
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();

	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ВыплатаЗарплатыВыплаты.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ ВТ_ТАБ
		|ИЗ
		|	Документ.ВКМ_ВыплатаЗарплаты.Выплаты КАК ВКМ_ВыплатаЗарплатыВыплаты
		|ГДЕ
		|	ВКМ_ВыплатаЗарплатыВыплаты.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	-ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток КАК Превышение,
		|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.Сотрудник.Представление КАК СотрудникПредставление
		|ИЗ
		|	РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками.Остатки(
		|			&Дата,
		|			СОтрудник В
		|				(ВЫБРАТЬ
		|					ВТ_ТАБ.Сотрудник КАК Сотрудник
		|				ИЗ
		|					ВТ_ТАБ КАК ВТ_ТАБ)) КАК ВКМ_ВзаиморасчетыССотрудникамиОстатки
		|ГДЕ
		|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток < 0";
	
	Запрос.УстановитьПараметр("Дата", Новый Граница(МоментВремени()));
	Запрос.УстановитьПараметр("Ссылка", Ссылка); 
	
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Проведение документа невозможно, сумма планируемой выплаты по сотруднику %1 превышает начисления на %2 р.",
				Выборка.СотрудникПредставление, Выборка.Превышение);
			Сообщение.Сообщить();
		КонецЦикла;
		
		Отказ = Истина;
		Возврат;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
#КонецЕсли
